#include "common/fullscreen_vs_inout.hlsli"

#define TONEMAP_GAMMA 2.224

Texture2D g_texture                             : register(t0, space0);
SamplerState g_sampler                          : register(s0, space0);

// Uncharted 2 Tonemapper
float3 tonemap_uncharted2(in float3 x)
{
    float A = 0.15;
    float B = 0.50;
    float C = 0.10;
    float D = 0.20;
    float E = 0.02;
    float F = 0.30;

    return ((x*(A*x+C*B)+D*E)/(x*(A*x+B)+D*F))-E/F;
}

float3 tonemap_uc2(in float3 color)
{
    float W = 11.2;

    color *= 16;  // Hardcoded Exposure Adjustment

    float exposure_bias = 2.0f;
    float3 curr = tonemap_uncharted2(exposure_bias*color);

    float3 white_scale = 1.0f/tonemap_uncharted2(W);
    float3 ccolor = curr*white_scale;

    float3 ret = pow(abs(ccolor), TONEMAP_GAMMA); // gamma

    return ret;
}

// Filmic tonemapper
float3 tonemap_filmic(in float3 color)
{
    color = max(0, color - 0.004f);
    color = (color * (6.2f * color + 0.5f)) / (color * (6.2f * color + 1.7f)+ 0.06f);

    // result has 1/2.2 baked in
    return pow(color, TONEMAP_GAMMA);
}

// MGS V tonemapper
float3 tonemap_mgsv(in float3 color)
{
    float A = 0.6;
    float B = 0.45333;
    float3 mapped = min(1, A + B - ( (B * B) / (color - A + B) ));
    float3 condition = float3(color.r > A, color.g > A, color.b > A);

    return mapped * condition + color * (1 - condition);
}

//=================================================================================================
//
//  Baking Lab
//  by MJP and David Neubelt
//  http://mynameismjp.wordpress.com/
//
//  All code licensed under the MIT license
//
//=================================================================================================

// The code in this file was originally written by Stephen Hill (@self_shadow), who deserves all
// credit for coming up with this fit and implementing it. Buy him a beer next time you see him. :)

// sRGB => XYZ => D65_2_D60 => AP1 => RRT_SAT
static const float3x3 ACESInputMat =
{
    {0.59719, 0.35458, 0.04823},
    {0.07600, 0.90834, 0.01566},
    {0.02840, 0.13383, 0.83777}
};

// ODT_SAT => XYZ => D60_2_D65 => sRGB
static const float3x3 ACESOutputMat =
{
    { 1.60475, -0.53108, -0.07367},
    {-0.10208,  1.10813, -0.00605},
    {-0.00327, -0.07276,  1.07602}
};

float3 RRTAndODTFit(float3 v)
{
    float3 a = v * (v + 0.0245786f) - 0.000090537f;
    float3 b = v * (0.983729f * v + 0.4329510f) + 0.238081f;
    return a / b;
}

float3 ACESFitted(float3 color)
{
    color = mul(ACESInputMat, color);

    // Apply RRT and ODT
    color = RRTAndODTFit(color);

    color = mul(ACESOutputMat, color);

    // Clamp to [0, 1]
    color = saturate(color);

    return color;
}

//
// dithering

uint wang_hash(uint seed)
{
    seed = (seed ^ 61) ^ (seed >> 16);
    seed *= 9;
    seed = seed ^ (seed >> 4);
    seed *= 0x27d4eb2d;
    seed = seed ^ (seed >> 15);
    return seed;
}

float wang_float(uint hash)
{
    return hash / float(0x7FFFFFFF) / 2.0;
}

float3 dither(in float3 color, float2 screen_pixel, int divisor) {
    uint seed = uint(screen_pixel.x) + uint(screen_pixel.y) * 8096;
    float r = wang_float(wang_hash(seed * 3 + 0));
    float g = wang_float(wang_hash(seed * 3 + 1));
    float b = wang_float(wang_hash(seed * 3 + 2));
    float3 random = float3(r, g, b);

    return color + ((random - .5) / divisor);
}


float4 main_ps(vs_out In) : SV_TARGET
{
    float3 hdr = g_texture.Sample(g_sampler, In.Texcoord).rgb;

    //if (In.Texcoord.x < 0.5f)
    //    return float4(hdr, 1.f);

    // tonemap
    float3 color = ACESFitted(hdr);

    // gamma
    color = pow(color, 1.0f / 2.2f);

    // dither
    color = dither(color, In.SV_P.xy, 100);

    return float4(color, 1.f);
}
